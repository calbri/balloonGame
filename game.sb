GraphicsWindow.BackgroundColor = "White"

balloonX = 0
balloonY = 0
balloonDiameter = 20
balloon = Shapes.AddEllipse(balloonDiameter, balloonDiameter)
balloonSpeed = 1
balloonDirection = 1

width = GraphicsWindow.Width
height = GraphicsWindow.Height

cannonBodyWidth = 50
cannonBarrelWidth = 15
cannonBarrelHeight = 10
cannonBodyHeight = 30
cannonX = GraphicsWindow.Width - cannonBodyWidth
cannonY = GraphicsWindow.Height - cannonBodyHeight
body = Shapes.AddRectangle(cannonBodyWidth, cannonBodyHeight)
barrel = Shapes.AddRectangle(cannonBarrelWidth, cannonBarrelHeight)
barrelX = cannonX - cannonBarrelWidth
barrelY = cannonY + ((cannonBarrelHeight + cannonBodyHeight) / 4)
Shapes.Move(body, cannonX, cannonY)
Shapes.Move(barrel, barrelX, barrelY)

GraphicsWindow.KeyDown = keyDown
GraphicsWindow.KeyUp = keyUp

cannonDirection = 0
cannonSpeed = 1

canShoot = 1
bulletSpeed = 0

missedShots = 0

bulletSize = 10
bullet = Shapes.AddRectangle(bulletSize, bulletSize)
bulletX = -bulletSize
bulletY = -bulletSize
Shapes.Move(bullet, bulletX, bulletY)

RunLoop:
  height = GraphicsWindow.Height

  balloonY = balloonY + balloonSpeed*balloonDirection
  
  If (balloonY + balloonDiameter >= height or balloonY <= 0) Then
    balloonDirection = -balloonDirection
    
    balloonY = balloonY + balloonSpeed*balloonDirection
  EndIf
  
  cannonY = cannonY + cannonSpeed*cannonDirection
  
  If (cannonY + cannonBodyHeight >= height or cannonY <= 0) Then
    cannonY = cannonY - cannonSpeed*cannonDirection
  EndIf
  
  barrelY = cannonY + ((cannonBarrelHeight + cannonBodyHeight) / 4)
  
  bulletX = bulletX - bulletSpeed
  
  If(bulletX <= -bulletSize) Then
    If canShoot = 0 Then
      missedShots = missedShots + 1
    EndIf
    canShoot = 1
    bulletSpeed = 0
  EndIf
  
  If Math.GetRandomNumber(30) = 30 Then
    balloonDirection = balloonDirection * -1
  EndIf
    
  Shapes.Move(balloon, balloonX, balloonY)
  Shapes.Move(body, cannonX, cannonY)
  Shapes.Move(barrel, barrelX, barrelY)
  Shapes.Move(bullet, bulletX, bulletY)
  Program.Delay(5)  
  
  If (bulletX <= balloonX + balloonDiameter) and (bulletX + bulletSize >= balloonX) and (bulletY <= balloonY + balloonDiameter) and (bulletY + bulletSize >= balloonY) Then
    GraphicsWindow.ShowMessage("Missed shots = " + missedShots, "You Win!")
  Else
    Goto RunLoop
  EndIf
  
  
  
  
Sub keyDown
  If GraphicsWindow.LastKey = "Up" Then
    cannonDirection = -1
  ElseIf GraphicsWindow.LastKey = "Down" Then
    cannonDirection = 1
  ElseIf GraphicsWindow.LastKey = "Space" Then
    shoot()
  EndIf
EndSub

Sub keyUp
  If GraphicsWindow.LastKey = "Up" Then
    cannonDirection = 0
  ElseIf GraphicsWindow.LastKey = "Down" Then
    cannonDirection = 0
  EndIf
EndSub

Sub shoot
  If canShoot = 1 Then
    canShoot = 0
    bulletX = barrelX
    bulletY = barrelY
    Shapes.Move(bullet, bulletX, bulletY)
    bulletSpeed = balloonSpeed * 1.5
  EndIf
EndSub
    